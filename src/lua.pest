WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

Plus = @{ "+" }
Minus = @{ "-" }
Asterisks = @{ "*" }
Slash = @{ "/" }
Percent = @{ "%" }
Circumflex = @{ "^" }
Hash = @{ "#" }
Ampersand = @{ "&" }
Tilde = @{ "~" }
VerticalBar = @{ "|" }
LeftShift = @{ "<<" }
RightShift = @{ ">>" }
DoubleSlash = @{ "//" }
DoubleEquals = @{ "==" }
Different = @{ "~=" }
LessOrEqual = @{ "<=" }
GreaterOrEqual = @{ ">=" }
Less = @{ "<" }
Greater = @{ ">" }
Equals = @{ "=" }
LeftParenthesis = @{ "(" }
RightParenthesis = @{ ")" }
LeftBrace = @{ "{" }
RightBrace = @{ "}" }
LeftBracket = @{ "[" }
RightBracket = @{ "]" }
DoubleColon = @{ "::" }
Semicolon = @{ ";" }
Colon = @{ ":" }
Comma = @{ "," }
TripleDot = @{ "..." }
DoubleDot = @{ ".." }
Dot = @{ "." }

And = @{ "and" }
Break = @{ "break" }
Do = @{ "do" }
Else = @{ "else" }
Elseif = @{ "elseif" }
End = @{ "end" }
False = @{ "false" }
For = @{ "for" }
Function = @{ "function" }
Goto = @{ "goto" }
If = @{ "if" }
In = @{ "in" }
Local = @{ "local" }
Nil = @{ "nil" }
Not = @{ "not" }
Or = @{ "or" }
Repeat = @{ "repeat" }
Return = @{ "return" }
Then = @{ "then" }
True = @{ "true" }
Until = @{ "until" }
While = @{ "while" }

Alpha = _{ 'a'..'z' | 'A'..'Z' }
Digit = _{ '0'..'9' }
HexDigit = _{ '0'..'9' | 'a'..'f' | 'A'..'F' }
HexPrefix = _{ "0" ~ ("x" | "X") }

HexExponent = _{ ("p" | "P") ~ ("-" | "+")? ~ HexDigit+ }
HexFloat1 = _{ "-"? ~ HexPrefix ~ HexDigit* ~ "." ~ HexDigit+ ~ HexExponent? }
HexFloat2 = _{ "-"? ~ HexPrefix ~ HexDigit+ ~ "." ~ HexDigit* ~ HexExponent? }
HexFloat3 = _{"-"? ~ HexPrefix ~ HexDigit+ ~ HexExponent}
HexFloat = @{ HexFloat1 | HexFloat2 | HexFloat3 }

HexInteger = @{ "-"? ~ HexPrefix ~ HexDigit+ }

Exponent = _{ ("e" | "E") ~ ("-" | "+")? ~ Digit+ }
Float1 = _{ "-"? ~ Digit* ~ "." ~ Digit+ ~ Exponent? }
Float2 = _{ "-"? ~ Digit+ ~ "." ~ Digit* ~ Exponent? }
Float3 = _{ "-"? ~ Digit+ ~ Exponent }
Float = @{ Float1 | Float2 | Float3 }

Integer = @{ "-"? ~ Digit+ }

Identifier = { ("_" | Alpha) ~ ("_" | Alpha | Digit)* }

SqString = ${"'" ~ SqInner ~ "'"}
SqInner = @{ SqChar* }
SqChar = _{ !("'" | "\\") ~ ANY | "\\" ~ ANY }

DqString = ${"\"" ~ DqInner ~ "\""}
DqInner = @{ DqChar* }
DqChar = _{ !("\"" | "\\") ~ ANY | "\\" ~ ANY }

RawString = ${ "[" ~ PUSH("="*) ~ "[" ~ RawInner ~ "]" ~ POP ~ "]" }
RawInner = @{ RawChar* }
RawChar = _{ !("]" ~ PEEK ~ "]") ~ ANY }

Integers = { SOI ~ Integer ~ ("," ~ Integer)* ~ EOI }
HexIntegers = { SOI ~ HexInteger ~ ("," ~ HexInteger)* ~ EOI }
Floats = { SOI ~ Float ~ ("," ~ Float)* ~ EOI }
HexFloats = { SOI ~ HexFloat ~ ("," ~ HexFloat)* ~ EOI }
Identifiers = { SOI ~ Identifier ~ ("," ~ Identifier)* ~ EOI }
SqStrings = { SOI ~ SqString ~ ("," ~ SqString)* ~ EOI }
DqStrings = { SOI ~ DqString ~ ("," ~ DqString)* ~ EOI }
RawStrings = { SOI ~ RawString ~ ("," ~ RawString)* ~ EOI }

Number = { HexFloat | Float | HexInteger | Integer }
String = { SqString | DqString | RawString }
Symbol = { Plus | Minus | Asterisks | Slash | Percent | Circumflex | Hash | Ampersand | Tilde | VerticalBar | LeftShift | RightShift | DoubleSlash | DoubleEquals | Different | LessOrEqual | GreaterOrEqual | Less | Greater | Equals | LeftParenthesis | RightParenthesis | LeftBrace | RightBrace | LeftBracket | RightBracket | DoubleColon | Semicolon | Colon | Comma | TripleDot | DoubleDot | Dot }
Keyword = { And | Break | Do | Else | Elseif | End | False | For | Function | Goto | If | In | Local | Nil | Not | Or | Repeat | Return | Then | True | Until | While }
All = { SOI ~ (Number | String | Symbol | Keyword)* ~ EOI }